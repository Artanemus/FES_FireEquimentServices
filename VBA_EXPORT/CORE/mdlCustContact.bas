Attribute VB_Name = "mdlCustContact"
Option Compare Database
Option Explicit

Const ModuleName As String = "mdlCustContact"


Private vReturnValue As Variant                  'Routines called by this module write to this param
Private mHRID As Variant
Private mCustContactID As Variant

Public Property Let ReturnValue(X As Variant)
    vReturnValue = X
End Property

Public Property Let HR_ID(X As Variant)
    mHRID = X
End Property

Public Property Get HR_ID() As Variant
    HR_ID = mHRID
End Property

Public Property Let CustContact_ID(X As Variant)
    mCustContactID = X
End Property

Public Property Get CustContact_ID() As Variant
    CustContact_ID = mCustContactID
End Property

Public Function RemoveCustContact(ByVal aCustContactID As Variant) As Boolean
Dim dbs As Database
Dim SQL As String
Dim Msg As String

    On Error GoTo PROC_ERR

    RemoveCustContact = False
    If Nz(aCustContactID, 0) > 0 Then
        SQL = "DELETE FROM dbo_CustContact WHERE CustContactID = " & CStr(aCustContactID)
        Set dbs = CurrentDb
        dbs.Execute SQL, dbFailOnError + dbSeeChanges
        RemoveCustContact = True
    End If
    
PROC_EXIT:
    Set dbs = Nothing
    On Error Resume Next
    Exit Function

PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = ModuleName & " RemoveCustContact" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
    
End Function


Public Function ExeCustAddContact(ByVal aCustID As Variant) As Boolean
    mHRID = Null
    vReturnValue = Null
    ExeCustAddContact = False
    If (Nz(aCustID, 0) > 0) Then
    
        ' select a HR from the picker return 0 if none picked
        mHRID = mdlPicker.PickHR_CustContact
        
        '########################################################################
        If mHRID = 0 Then
            ' extended function of mdlPicker ... user pressed 'Create New Contact'
            If mdlPicker.DoCreateNewContact = 1 Then
                ' -------------------------------------------------------------
                ' Create New Contact Button ...
                ' -------------------------------------------------------------
                
                ' BSA TEST
                ' remove from memory
'                If IsLoaded("xDlgCustAddNewContact") Then
'                   ' Unload Forms("xDlgCustAddNewContact")
'                    'DoCmd.Close acForm, "xDlgCustomerAddNewContact"
'                    DoCmd.GoToRecord acDataForm, "xDlgCustAddNewContact", acNewRec
'                    Forms("xDlgCustAddNewContact").Visble = True
'                Else
'                    ' execute the add new contact Dialogue ....
'                End If
                
                DoCmd.OpenForm "xDlgCustAddNewContact", acAdd, , , , acDialog
                ' Access holds the form in cache and doesn't kill it
                ' HENCE IT DOESN'T PERFORM NEW RECORD CORRECTLY
                ' this requery - before the form is closed fixes that problem....
                ' note: trap for - kill process on menu bar
                If IsLoaded("xDlgCustAddNewContact") Then
                    Forms("xDlgCustAddNewContact").Form.Requery
                End If
                
                DoCmd.Close acForm, "xDlgCustomerAddNewContact"
                ' the form isn't released from cache ... Unload..DeActivate..Close
                ' true
                DoEvents
                
                
                If Nz(vReturnValue, 0) > 0 Then
                    ' return value holds the newly created HRID
                    mHRID = vReturnValue
                    'create a new contactlink record for the customer
                    addNewCustContact mHRID, CLng(aCustID)
                        
                    ' +---- STATE OF Module PARAMS ---------------------------+
                    ' mHRID = HR to include in customer list
                    ' mCustContactID = new contact linklist ID
                    ' +-------------------------------------------------------+
                    ExeCustAddContact = True     ' return success
                
                Else
                    ' USER ABORTED CREATENEWCONTACT ... NO HRID PICKED
                    ' +---- STATE OF Module PARAMS ---------------------------+
                    mHRID = Null                 ' Assert
                    mCustContactID = Null
                    ' +-------------------------------------------------------+
                    ExeCustAddContact = False    ' return user aborted
                
                End If

            Else
                ' USER ABORTED PICKER ... NO HRID PICKED
                ' +---- STATE OF Module PARAMS ---------------------------+
                mHRID = Null                     ' Assert
                mCustContactID = Null
                ' +-------------------------------------------------------+
                ExeCustAddContact = False        ' return user aborted
            End If
            
            
            '########################################################################
        Else
            If TestForCustContact(mHRID, aCustID) = False Then
                'create a new contactlink record for the customer
                addNewCustContact mHRID, CLng(aCustID)
            
                ' +---- STATE OF Module PARAMS ---------------------------+
                '   mHRID = an existing HR to include in customer list
                '   mCustContactID = new contact linklist ID
                ' +-------------------------------------------------------+
                ExeCustAddContact = True         ' return success
            Else
                ' HR is already a contact for the customer
                MsgBox "The HR is already a contact for this customer!" & _
                vbCrLf & "Check that the contact isn't archived.", vbOKOnly Or vbExclamation, "Contact already exists."
            End If
        End If
        
    End If
End Function

Public Function TestForCustContact(ByVal aHRID As Long, ByVal aCustomerID As Long) As Boolean
    Dim s As String
    Dim SQL As String
    Dim rst As DAO.Recordset
    Dim dbs As DAO.Database
    Dim Msg As String

    On Error GoTo PROC_ERR
    
    TestForCustContact = False
    
    If aHRID > 0 And aCustomerID > 0 Then
        'create a new contactlink record for the customer
        Set dbs = CurrentDb
        SQL = "Select * FROM dbo_CustContact WHERE CustomerID = " & CStr(aCustomerID) & " AND HRID = " & CStr(aHRID)
        ' BSA correct method to use DAO to connect to MSSQL
        ' else use
        Set rst = dbs.OpenRecordset(SQL, dbOpenSnapshot, dbFailOnError + dbSeeChanges)
        If rst.RecordCount > 0 Then
            TestForCustContact = True
        End If
    End If
    
PROC_EXIT:
    'Cleanup
    Set rst = Nothing
    On Error Resume Next
    Exit Function

PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = ModuleName & " TestForCustContact" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
End Function

' +---------------------------------------------
' Insert a HR into the customer contact list
' +---------------------------------------------

Public Sub addNewCustContact(ByVal aHRID As Long, ByVal aCustomerID As Long)
    Dim s As String
    Dim SQL As String
    Dim rst As DAO.Recordset
    Dim dbs As DAO.Database
    Dim Msg As String

    On Error GoTo PROC_ERR
    
    If aHRID > 0 And aCustomerID > 0 Then
        'create a new contactlink record for the customer
        Set dbs = CurrentDb
        SQL = "Select * FROM dbo_CustContact"
        Set rst = dbs.OpenRecordset(SQL, dbOpenDynaset, dbFailOnError + dbSeeChanges)
        If Not IsNull(rst) Then
            With rst
                .AddNew
                .Fields("HRID") = aHRID
                .Fields("CustomerID") = aCustomerID
                .Fields("CreatedOn") = Now()
                .Fields("Caption") = "Contact added on " & Format(Date, "dd/mmm/yyyy")
                'TODO Change GUI to allow for contact type assignment
                .Fields("ContactTypeID") = 1             'PRIMARY ...
                .Fields("IsArchived") = False
                .Update
                .Bookmark = .LastModified
                mCustContactID = ![CustContactID]
            End With
        End If
        rst.Close
    End If
    
PROC_EXIT:
    'Cleanup
    Set rst = Nothing
    On Error Resume Next
    Exit Sub

PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = ModuleName & " addNewCustContact" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
End Sub




