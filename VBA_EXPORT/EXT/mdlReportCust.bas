Attribute VB_Name = "mdlReportCust"
Option Compare Database
'@Folder "CUSTOMER"
Option Explicit

Private Const ModuleName As String = "mdlReportCust"
Private vReturnValue As Variant

Public Property Get ReturnValue() As Variant
    If IsObject(vReturnValue) Then
        Set ReturnValue = vReturnValue
    Else
        ReturnValue = vReturnValue
    End If
End Property

Public Property Let ReturnValue(ByVal RHS As Variant)
    vReturnValue = RHS
End Property

Private Function HasTestResults(ByVal InspectionOrderID As Long) As Boolean
    Dim SQL As String, msg As String
    Dim rst As DAO.Recordset
    
    On Error GoTo PROC_ERR
    
    HasTestResults = False
    
    SQL = _
    "SELECT dbo_InspectionOrder.InspectionOrderID, Count(dbo_Test.TestID) AS CountOfTestID " & _
    "FROM dbo_InspectionOrder INNER JOIN dbo_Test ON dbo_InspectionOrder.InspectionOrderID = dbo_Test.InspectionOrderID " & _
    "GROUP BY dbo_InspectionOrder.InspectionOrderID " & _
    "HAVING dbo_InspectionOrder.InspectionOrderID = " & CStr(InspectionOrderID) & ";"
    
    Set rst = CurrentDb.OpenRecordset(SQL, dbOpenSnapshot, dbFailOnError + dbSeeChanges)
    If rst.BOF And rst.EOF Then
        GoTo PROC_EXIT
    Else
        HasTestResults = True
    End If

PROC_EXIT:
    On Error Resume Next
    Exit Function

PROC_ERR:

    ' display the system error
    If err.Number <> 0 Then
        msg = ModuleName & " SendCustEmail" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.SOURCE & vbCrLf & err.Description
        MsgBox msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
    
    
End Function

Private Function SaveCustEmail( _
        ByVal BCC As String, _
        ByVal FileToAttach As String, _
        ByVal InspectionOrderID As Long, _
        Optional ByVal RunSilent As Boolean = True) As Boolean
        
    Dim aArray_To() As Variant
    Dim aArray_Cc() As Variant
    Dim aArray_Bc() As Variant
    Dim aArray_Attachments() As Variant
    Dim v As Variant, v1 As Variant, v2 As Variant
    Dim s As String
    Dim mSender_FName As String, mSender_EmailAdr As String, mSender_HRID As Long
    Dim CustEmailAddress As String
    Dim Email As clsEmail
    Dim msg As String
    Dim mInspectO_Info As String
    Dim strSQL As String
    Dim rst As DAO.Recordset
    Dim mSubject As String
    Dim mBody As String
    Dim CustContactID As Long


    On Error GoTo PROC_ERR
    SaveCustEmail = False
    
    
    Set Email = New clsEmail
    CustEmailAddress = vbNullString
    mSender_EmailAdr = vbNullString
    CustContactID = 0
    
    ' Use the global BCC address to track and archive email traffic
    If Len(BCC) > 0 Then
        ReDim aArray_Bc(1)
        aArray_Bc(0) = BCC
    End If
    
    ' ---------------------------------
    ' INCLUDE USEFUL INSPECTION ORDER INFORMATION
    ' ---------------------------------
    mInspectO_Info = vbNullString
    strSQL = _
           "SELECT dbo_InspectionOrder.InspectionOrderID, dbo_InspectionOrder.CustomerID, dbo_InspectionOrder.SiteID, " & _
           "dbo_InspectionOrder.RequestedDT, dbo_InspectionOrder.InspectedOn, dbo_Customer.CustName, dbo_Site.Address " & _
           "FROM (dbo_InspectionOrder INNER JOIN dbo_Customer ON dbo_InspectionOrder.CustomerID = dbo_Customer.CustomerID) " & _
           "INNER JOIN dbo_Site ON dbo_InspectionOrder.SiteID = dbo_Site.SiteID " & _
           "WHERE (((dbo_InspectionOrder.InspectionOrderID)=" & CStr(InspectionOrderID) & ")); "

    Set rst = CurrentDb.OpenRecordset(strSQL, dbOpenSnapshot, dbFailOnError + dbSeeChanges)
    If rst.BOF And rst.EOF Then
        ' nothing to send
        GoTo PROC_EXIT
    Else
        ' SLUG LINE FOR EMAIL
        mSubject = "IDFES Inspection Order"
        ' INFO ABOUT THE INSPECTION ORDER - USED IN EMAIL BODY
        ' INSPECTION ORDER NUMBER
        mInspectO_Info = mInspectO_Info & "Ref. Inspection Order: " & CStr(Nz(rst.Fields("InspectionOrderID"), vbNullString)) & vbCrLf
        ' CUSTOMER ID AND FULLNAME
        mInspectO_Info = mInspectO_Info & "Customer: (" & CStr(rst.Fields("CustomerID")) & ") " & CStr(rst.Fields("CustName")) & vbCrLf
        ' SITE ID AND SITE ADDRESS (MULTILINE)
        ' MAKE SINGLE LINE
        ' TODO: use dbo scalar function to retrive address
        s = PrettyAddress(CStr(rst![Address]))
        mInspectO_Info = mInspectO_Info & "Site: (" & CStr(rst.Fields("SiteID")) & ") " & s & vbCrLf
        ' TODO: DATE OF INSPECTION - EITHER DAY OF INSPECTION OR 'REQUESTED ON' DATE BY ADMINISTRATION
        ' PICKDATE
        mInspectO_Info = mInspectO_Info & "Date: " & Format(Nz(rst![RequestedDT], 0), "dddd dd/mm/yy hhnn")
        
        '------------------------------------------------------
        ' CUSTOMER PREFERRED BUSINESS EMAIL ADDRESS TO SEND TO
        ' TODO: What is the customer's preferred email address?
        ' LOCATE PRIMARY ADDRESS
        '------------------------------------------------------
        ReDim aArray_To(1) ' assign single element
        
        v = [_QRY_SQLEXPRESS].GetCustEmail(CLng(rst.Fields("CustomerID")))
        If Len(Nz(v, vbNullString)) > 0 Then
            CustEmailAddress = CStr(v)
            ' BASE ZERO
            aArray_To(0) = CustEmailAddress
        End If
        v = [_QRY_SQLEXPRESS].GetCustContactID(CLng(rst.Fields("CustomerID")))
        If Not IsNull(v) Then
            CustContactID = v
        End If
        
    End If
    rst.Close
    Set rst = Nothing
    
    
    ' OPENING TITLE - double lined spaced
    mBody = "Ipswich District Fire and Emergency Services." & vbCrLf
    mBody = mBody & "Customer's Inspection Order - Results" & vbCrLf & vbCrLf
    ' GENERAL INFO  - double lined spaced
    If Len(mInspectO_Info) > 0 Then
        mBody = mBody & mInspectO_Info & vbCrLf & vbCrLf
    End If
    ' MAIN CLIENT LETTER AREA - including metadata
    ' TODO: Get text and metadata for the Customer Inspection Order email
    If CustContactID > 0 Then
        ' Disable Uppercase in LastName
        v = mdlHR.HR_GetFullName(CustContactID, 0)
    End If
    ' Include the salutation
    If Not IsNull(v) Then
        mBody = mBody & "ATTN: " & CStr(v)
    End If
    mBody = mBody & vbCrLf
    mBody = mBody & "Please find attached your copy of the IDFES Inspection Results. " & vbCrLf
    mBody = mBody & "Keep this report for QFES review and site verification. " & vbCrLf
    mBody = mBody & "Bests regards" & vbCrLf
    mBody = mBody & "IDFES administrator " & mSender_FName & vbCrLf
    mBody = mBody & "CONTACT EMAIL : " & mSender_EmailAdr
    
    ' THE FOLLOWING LOWER BAR INFO WILL BE INSERTED BY OUTLOOK AS THE AUTO_FOOTER TEMPLATE
    '    mBody = mBody & "CONTACT DETAILS" & vbCrLf
    '    mBody = mBody & "IDFES office number #" & mCompany_OfficeNumber & vbCrLf
    '    mBody = mBody & "IDFES contact email #" & mCompany_EmailAddress & vbCrLf
        
    Email.Body = mBody
    Email.Subject = mSubject
    
    
    ' ----------------------------------------------------
    ' ATTACHMENTS
    ' ----------------------------------------------------
    If Len(FileToAttach) > 0 Then
        ReDim aArray_Attachments(1)
        aArray_Attachments(0) = FileToAttach
    End If
'    If Len(FileToAttach2) > 0 Then
'        ReDim Preserve aArray_Attachments(2)
'        aArray_Attachments(1) = FileToAttach2
'    End If
    
    'Email.Attachment = FileToAttach
    
    ' ----------------------------------------------------
    ' GO CREATE THE DRAFT EMAIL
    ' ----------------------------------------------------
    SaveCustEmail = Email.exeSaveEmail(aArray_To, aArray_Cc, aArray_Bc, aArray_Attachments)
    ' FREE CLASS
    Set Email = Nothing
    
    If Not RunSilent Then
        If SaveCustEmail Then
            s = "A draft email was successfully created."
            MsgBox s, vbInformation Or vbOKOnly, "Email Customers Inspection Order."
        Else
            s = "An Outlook error occurred when creating the draft email."
            MsgBox s, vbExclamation Or vbOKOnly, "Email Customers Inspection Order."
        End If
    End If
        
PROC_EXIT:
    On Error Resume Next
    If Not Email Is Nothing Then
        Set Email = Nothing
    End If
    Exit Function

PROC_ERR:

    ' display the system error
    If err.Number <> 0 Then
        msg = ModuleName & " SendCustEmail" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.SOURCE & vbCrLf & err.Description
        MsgBox msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
End Function

Public Function exeReportCust() As Boolean
    Dim obj As AccessObject
    Dim rst As DAO.Recordset
    Dim ProgBarForm As frmProgress
    Dim frm As Form_DlgReportCust
    Dim ctrl As Access.Control
    
    Dim RootPath As Variant, v As Variant
    Dim success As Boolean, EnableEmail As Boolean
    Dim lngInspectionOrderID As Long, lngCustomerID As Long, lngInspectionOrderStatusID As Long
    Dim intYear As Integer, intMonth As Integer, intDay As Integer
    Dim intIndex As Integer, msgBoxRexults As Integer
    Dim SQL As String, WhereClause As String, outputFile As String, msg As String
    Dim strInspectOrder As String, strCustID As String, strFolder As String, CustCode As String
    Dim StartDate As Variant, EndDate As Variant
    Dim s As String, DateRangeStr As String, EmailAddressStr As String, UserOutputFolder As String
    Dim SenderName As String
    Dim CheckTestResults As Boolean
    Dim GroupedSubFolders As Boolean
    
    On Error GoTo PROC_ERR
    
    exeReportCust = False

    DoCmd.OpenForm "DlgReportCust", , , , , acDialog
    
    If vReturnValue = 0 Then
        DoCmd.Close acForm, "DlgReportCust", acSaveNo
        Exit Function                            ' user Aborted
    End If
    
    ' ------------------------------------------------------------------------------------------------------
    ' START - EXTRACT USER'S INPUT DATA FROM FORM DlgReportCust
    ' ------------------------------------------------------------------------------------------------------
    Set frm = Forms("DlgReportCust")
    ' quick - unsafe method to get params
    With frm
        Set ctrl = .Controls("cmbxCustomerID")
        ' BOUND to COL 0 - NO DEFAULT VALUE - EXPECT NULL
        If Not IsNull(ctrl) Then
            lngCustomerID = ctrl.Column(0)
        Else
            ' DISPLAY ALL CUSTOMERS FOR DATE RANGE....
            lngCustomerID = 0
        End If
        
        Set ctrl = .Controls("cmbxInspectOrderStatusID")
        ' BOUND to COL 0 - GIVEN DEFAULT VALUE AT DESIGN TIME = 1
        If Not IsNull(ctrl) Then
            lngInspectionOrderStatusID = ctrl.Column(0)
        Else
            lngInspectionOrderStatusID = 1
        End If
        
        Set ctrl = .Controls("optgDateRange")
        ' GIVEN DEFAULT VALUE AT DESIGN TIME
        intIndex = ctrl.Value
        
        Select Case intIndex
        Case 1
            ' SELECT MONTH THIS YEAR
            Set ctrl = .Controls("cmbxMonths")
            If IsNull(ctrl) Or ctrl.ListIndex = -1 Then
                ' no input from user - calc the current month
                StartDate = DateSerial(DatePart("yyyy", Date), DatePart("m", Date), 1)
                EndDate = DateSerial(DatePart("YYYY", Date), DatePart("m", Date) + 1, 1)
                ' less a day equals last day of the previous month.
                EndDate = DateAdd("d", -1, EndDate)
            Else
                ' user selects month from combobox
                intMonth = ctrl.Column(0)        '
                StartDate = DateSerial(DatePart("yyyy", Date), intMonth, 1)
                EndDate = DateSerial(DatePart("YYYY", Date), intMonth + 1, 1)
                ' less a day equals last day of the previous month.
                EndDate = DateAdd("d", -1, EndDate)
            End If
        Case 2
            ' SELECT DATE RANGE - START\ENDDATE ARE VARIANT
            Set ctrl = .Controls("edtStartDate")
            StartDate = ctrl.Value
            
            Set ctrl = .Controls("edtEndDate")
            If IsNull(ctrl) Then
                EndDate = Date                   ' as of today - both month and day ....
            Else
                EndDate = ctrl.Value
            End If
            
        End Select
            
        
        ' CHECK BOXES ... (ALL GIVEN DEFAULT VALUES AT DESIGN TIME)
        Set ctrl = .Controls("chkEnableEmail")
        EnableEmail = ctrl.Value
        
        Set ctrl = .Controls("chkTestResults")
        CheckTestResults = ctrl.Value
        
        Set ctrl = .Controls("chkGroupedSubFolders")
        GroupedSubFolders = ctrl.Value
        
        ' a user defined output path
        UserOutputFolder = vbNullString
        Set ctrl = .Controls("txtOutputFolder")
        If Not IsNull(ctrl) Then
            UserOutputFolder = ctrl.Value
        End If
        
        ' ASSERT DATERANGE
        
    End With
    ' finished with dialogue
    DoCmd.Close acForm, "DlgReportCust", acSaveNo
    ' ------------------------------------------------------------------------------------------------------
    ' END - EXTRACT USER'S INPUT DATA FROM FORM DlgReportCust
    ' ------------------------------------------------------------------------------------------------------
    
    
    ' ------------------------------------------------------------------------------------------------------
    ' START - CHECK USER INPUT DATA
    ' ------------------------------------------------------------------------------------------------------
    ' ABORT ON NULL OR BAD DATE RANGE
    If IsNull(EndDate) Or Not IsDate(EndDate) Then
        MsgBox "Bad end date. Abort.", vbOKOnly Or VbMsgBoxStyle.vbCritical, "Batch Print"
        Exit Function                            ' UNEXPECTED ERROR
    End If
    If IsNull(StartDate) Or Not IsDate(StartDate) Then
        MsgBox "Bad start date. Abort.", vbOKOnly Or VbMsgBoxStyle.vbCritical, "Batch Print"
        Exit Function                            ' UNEXPECTED ERROR
    End If
    
    ' adjust time to end at midnight - so date range is fully 'inclusive'.
    EndDate = mdlToolBox.AdjustDTCeiling(EndDate)
    
    If lngInspectionOrderStatusID = 0 Then
        MsgBox "Bad Inspection Status ID. Abort.", vbOKOnly Or VbMsgBoxStyle.vbCritical, "Batch Print"
        Exit Function                            ' UNEXPECTED ERROR
    End If

    If Len(UserOutputFolder) = 0 Then
        ' use the FES global output path ...
        RootPath = mdlToolBox.GetBatchPrintRootPath()
        If Len(RootPath) = 0 Then
            Exit Function                        ' UNEXPECTED ERROR
        End If
        RootPath = RootPath & "CustInspectionOrders\"
    Else
        ' use the user's select path...
        RootPath = UserOutputFolder
    End If
    ' ------------------------------------------------------------------------------------------------------
    ' END - CHECK USER INPUT DATA
    ' ------------------------------------------------------------------------------------------------------
    
    
    ' BUILD OUTPUT DIRECTORY ....
    ' VBA IS PRIMATIVE - WE MUST CHECK & BUILD EACH SUB-FOLDER SEPERATELY
    ' BUILD THE TECH FOLDER TO DUMP INSPECTION ORDERS INTO
    If Len(Dir(RootPath, vbDirectory)) = 0 Then
        ' create the folder
        MkDir RootPath
    End If

    ' BUILD SQL - WHERE CLAUSE - DATE RANGE CRITERIA
    WhereClause = "( " & _
                  "([RequestedDT] >= #" & Format(CDate(StartDate), "mm/dd/yyyy") & "#) " & _
                  " AND " & _
                  "([RequestedDT] < #" & Format(CDate(EndDate), "mm/dd/yyyy hh:nn") & "#)" & _
                  ")"
            
    ' BUILD SQL - CORE + ATTACH WHERE CLAUSE
    SQL = _
        "SELECT dbo_InspectionOrder.InspectionOrderID, dbo_InspectionOrder.CustomerID, " & _
        "dbo_InspectionOrder.ServiceInterval, dbo_HR.LastName, dbo_HR.FirstName, " & _
        "dbo_InspectionOrder.RequestedDT " & _
        "FROM dbo_InspectionOrder " & _
        "LEFT JOIN dbo_HR ON dbo_InspectionOrder.HRID = dbo_HR.HRID " & _
        " WHERE " & WhereClause & " AND InspectionStatusID = " & CStr(lngInspectionOrderStatusID)
            
    ' BUILD SQL - WHERE CLAUSE - FILTER ON CUSTOMER ID
    If lngCustomerID > 0 Then
        SQL = SQL & " AND dbo_InspectionOrder.HRID = " & CStr(lngCustomerID)
    End If

    ' PREPARE - PROGRESS BAR - CONSTRUCTION
    Set ProgBarForm = New frmProgress            'max=100 value=0 dosmooth=true
    If Not ProgBarForm Is Nothing Then
        ProgBarForm.xInitialize
        Load ProgBarForm
        ProgBarForm.lblTitle.Caption = "Batch Print Customer's Inspection Report."
        ProgBarForm.lblCaption.Caption = "..."
        ProgBarForm.BorderStyle = fmBorderStyleSingle
        ProgBarForm.StartUpPosition = 2
        ' don't display the progress dialogue until after the confirm dlg
        'ProgBarForm.Show
        DoEvents
    End If
        
    ' ------------------------------------------------------------------------------------------------------
    ' START - RECORDSET - OPEN QUERY ...
    ' ------------------------------------------------------------------------------------------------------
    Set rst = CurrentDb.OpenRecordset(SQL, dbOpenSnapshot, dbFailOnError + dbSeeChanges)
    If rst.RecordCount > 0 Then
        
        rst.MoveLast
    
        ' PLEASE CONFIRM BATCH REPORT
        DateRangeStr = Format(CDate(StartDate), "mm/dd/yyyy") & " - " & Format(CDate(EndDate), "mm/dd/yyyy")
        EmailAddressStr = GetUser_AdminEmailAddress(SenderName)
        
        If EnableEmail Then
            msg = "Enabled."
        Else
            msg = "Not enabled."
        End If
                                                
        s = str(rst.RecordCount) & "|" & _
            Format(CDate(StartDate), "dd/mm/yyyy") & " - " & Format(CDate(EndDate), "dd/mm/yyyy") & _
            "|" & msg & "|" & RootPath
        
        DoCmd.OpenForm "DlgReportCustConfirm", , , , , acDialog, s
        DoCmd.Close acForm, "DlgReportCustConfirm", acSaveNo
        
        If vReturnValue = 0 Then
            GoTo PROC_EXIT                       ' USER ABORTED
        Else
            ' display the progressDlg
            If Not ProgBarForm Is Nothing Then
                ProgBarForm.xMax = rst.RecordCount
                ProgBarForm.Show
                DoEvents
            End If
        End If
    
        rst.MoveFirst
                        
        ' IF REPORT IS MOUNTED - CLOSE IT
        Set obj = Application.CurrentProject.AllReports("_rpt_CustRpt")
        If Not obj Is Nothing Then
            If obj.IsLoaded Then
                ' LOAD A BLANK CUSTOMER'S INSPECTION REPORT
                'DoCmd.OpenReport "_rpt_CustRpt", acViewReport, , , acHidden, "0|1"
                DoCmd.Close acReport, "_rpt_CustRpt", acSaveNo
            End If
        End If
        Set obj = Nothing
                        
        ' PROGRESS BAR - Ready to output - ENABLE ESCAPE BATCH PRINT
        If Not ProgBarForm Is Nothing Then
            ProgBarForm.lblCaption.Caption = "Output of reports in progress..."
            ProgBarForm.cmdCancel.Visible = True
            DoEvents
        End If
        
        
        ' ------------------------------------------------------------------------------------------------------
        ' START - MAIN LOOP ACROSS QUERY ...
        ' ------------------------------------------------------------------------------------------------------
            
        Do While Not rst.EOF
        
            If CheckTestResults = True Then
                If HasTestResults(rst.Fields("InspectionOrderID")) = False Then
                    GoTo SKIP_001
                End If
            End If
                
            strInspectOrder = Format(rst.Fields("InspectionOrderID"), "0000")
            strCustID = Format(rst.Fields("CustomerID"), "0000")
            
            If Not ProgBarForm Is Nothing Then
                ProgBarForm.lblCaption.Caption = "Output IO: " & strInspectOrder & " CustomerID: " & strCustID
                DoEvents
            End If
            
            If GroupedSubFolders Then
                ' VBA IS PRIMATIVE - WE MUST CHECK & BUILD EACH FOLDER SEPERATELY
                intYear = DatePart("yyyy", rst.Fields("RequestedDT"))
                strFolder = RootPath & Format(intYear, "0000") & "\"
                If Len(Dir(strFolder, vbDirectory)) = 0 Then
                    ' create the folder
                    MkDir strFolder
                End If
                    
                ' VBA IS PRIMATIVE - WE MUST CHECK & BUILD EACH FOLDER SEPERATELY
                intMonth = DatePart("m", rst.Fields("RequestedDT"))
                strFolder = strFolder & Format(intMonth, "00") & "\"
                If Len(Dir(strFolder, vbDirectory)) = 0 Then
                    ' create the folder
                    MkDir strFolder
                End If
            Else
                strFolder = RootPath
            End If
                
            CustCode = vbNullString
            v = DLookup("[CustCode]", "dbo_Customer", "[CustomerID] = " & CStr(rst.Fields("CustomerID")))
            If (Len(Nz(v, "")) > 0) Then
                CustCode = CStr(v) & "_"
            End If

            outputFile = strFolder & "CustRPT_" & CustCode & strInspectOrder & "_" & strCustID & ".pdf"
                
            ' if ACCESS throw exception error while processing the report - skip to next record.
            On Error GoTo SKIP_001
            'acFormatPDF, RTF, HTML, SNP, TXT, XLS, XPS
            ' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
            
            DoCmd.OpenReport "_rpt_CustRpt", acViewReport, , , acHidden, CStr(rst.Fields("InspectionOrderID")) & "|" & CStr(rst.Fields("ServiceInterval"))
            DoCmd.OutputTo acOutputReport, "_rpt_CustRpt", acFormatPDF, outputFile, False, "", , acExportQualityPrint
            DoCmd.Close acReport, "_rpt_CustRpt", acSaveNo
            ' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
                    
            ' SEND TEMPLATE EMAIL TO FES ADMINISTRATOR
            ' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
            If EnableEmail Then
                ' GET BCC email address
                v = mdlCompany.GetGlobalBCCEmailAddress()
                ' send the email to office admin
                success = SaveCustEmail(v, outputFile, rst.Fields("InspectionOrderID"))
            End If
            ' +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
SKIP_001:
            On Error GoTo PROC_ERR
            
            
            ' XXXXXXXXXXXXXXXXXXXXX
            If Not ProgBarForm Is Nothing Then
                ProgBarForm.xIncrement
                DoEvents
                
                '  ABORT OUTPUT OF REPORTS
                If ProgBarForm.xAbort = True Then
                    ProgBarForm.lblCaption.Caption = "Aborting Batch Print..."
                    DoEvents
                    Exit Do
                End If

            End If
            
                
            rst.MoveNext
        Loop
        ' ------------------------------------------------------------------------------------------------------
        ' END - MAIN LOOP ACROSS QUERY ...
        ' ------------------------------------------------------------------------------------------------------
        
        ' Unload recordset
        rst.Close
        Set rst = Nothing
                
        exeReportCust = True                     ' FLAG - SUCCESS
        
        ' HIDE VISIBILITY OF PROGRESS BAR
        If Not ProgBarForm Is Nothing Then
            ProgBarForm.Hide
        End If
        
    Else
        ' -------------------------------------------------------------------------------------------------
        ' NO RECORDS FOUND.... THE RECORDSET WAS EMPTY
        If Not ProgBarForm Is Nothing Then
            ProgBarForm.Hide
        End If
        ' Unload recordset
        If Not rst Is Nothing Then
            rst.Close
        End If
        Set rst = Nothing
        MsgBox "Nothing to process. No matches found.", vbOKOnly Or VbMsgBoxStyle.vbInformation, "Batch Print"

    End If
    ' ------------------------------------------------------------------------------------------------------
    ' END - RECORDSET - CLOSE QUERY ...
    ' ------------------------------------------------------------------------------------------------------
    
        
PROC_EXIT:
    On Error Resume Next
    
    ' PROGRESS BAR - RELEASE USERFORM MEMORY
    ' Performed prior to final message box
    If Not ProgBarForm Is Nothing Then
        ' ASSERT VISIBILITY ...
        If ProgBarForm.Visible = True Then
            ProgBarForm.Hide
        End If
        ' CLEAN MEMORY
        Unload ProgBarForm
        Set ProgBarForm = Nothing
    End If
    
    ' only shown if no errors
    If exeReportCust Then
        MsgBox "Batch Print - completed ", vbOKOnly, "Batch Print"
    End If
    
    Exit Function

PROC_ERR:
    ' PROGRESS BAR - CLEAR THE SCREEN
    If Not ProgBarForm Is Nothing Then
        ProgBarForm.Hide
    End If
    
    ' display the system error
    If err.Number <> 0 Then
        msg = ModuleName & " exeReportCust" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.SOURCE & vbCrLf & err.Description
        MsgBox msg, , "Error", err.HelpFile, err.HelpContext
    End If
    Resume PROC_EXIT
    
End Function


