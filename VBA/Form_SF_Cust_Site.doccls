Option Compare Database
Option Explicit

Const ModuleName As String = "SF_Cust_Site"
Const PKey As String = "[CustSiteID]"


Public Sub CueToRec(aCustSiteID As Long)
    Dim strCriteria As String
    Dim rst As DAO.Recordset
    Dim Msg As String
    On Error GoTo PROC_ERR
    
    Set rst = Me.RecordsetClone
    strCriteria = "[CustSiteID] = " & CStr(aCustSiteID)
    rst.FindFirst strCriteria
    If Not rst.NoMatch Then
        Me.Bookmark = rst.Bookmark
       'Me.SetFocus
        Exit Sub
    End If
    Set rst = Nothing

PROC_EXIT:
    On Error Resume Next
    Exit Sub

PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = ModuleName & " CueToRec" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
    
    Resume PROC_EXIT
    
End Sub

Private Sub cmdClearPostcode_Click()
    Dim lngPK As Long
    Dim intResponse As Integer
    Dim cCode As String
    
    lngPK = Me.CustSiteID
    If Nz(Me.[CustSiteID], 0) <> 0 Then
        If Nz(Me.[PostCodeID], 0) <> 0 Then
            cCode = mdlCompany.GetCompanyCode
            intResponse = MsgBox("Do you want to clear the postcode information " & _
                                 vbCrLf & "for the current selected Customer's Site?", _
                                 vbYesNo Or vbQuestion Or vbDefaultButton2, _
                                 cCode & " - Clear Postcode Information")
           
            If intResponse = vbYes Then
                Me.[PostCodeID] = Null
                'Me.Requery
        
                mdlToolBox.RequeryFormAndKeepCurrSelectedRecord Me.Form
                '-------------------------------------------------
'                With Me.RecordsetClone
'                    .FindFirst "[CustSiteID]=" & lngPK
'                    If Not .NoMatch Then
'                        If Me.Dirty Then
'                            Me.Dirty = False
'                        End If
'                        Me.Bookmark = .Bookmark
'                    End If
'                End With
            End If
        Else
            Beep
        End If
    End If
End Sub

Private Sub cmdGetPostcode_Click()
    Dim aPostCodeID As Variant
    Dim Msg As String
    
    aPostCodeID = Null
    
    On Error GoTo PROC_ERR
    If Me.Dirty Then
        Me.Dirty = False
    End If
    
    If Nz([CustSiteID], 0) <> 0 Then
        ' required - else locked in record.
        aPostCodeID = mdlPicker.PickPostcode()
        If (Nz(aPostCodeID, 0) <> 0) Then
            ' fill out the values in SF_Cust_Address
            Me.[PostCodeID] = aPostCodeID
            If Me.Dirty Then
                Me.Dirty = False
            End If
        End If
    End If
    
PROC_EXIT:
    On Error Resume Next
    Exit Sub

PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = "cmdGetPostcode_Click" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
    
    Resume PROC_EXIT
End Sub

Private Sub Form_BeforeInsert(Cancel As Integer)
    ' set the creation date for this customer
'    If Nz(Me![CreatedOn], 0) = 0 Then
'        Me![CreatedOn] = Now()
'    End If
    If IsNull(Me![CreatedOn]) Then
        Me![CreatedOn] = Now()
    End If

    If IsNull(Me![IsArchived]) Then
        Me![IsArchived] = False
    End If
    
End Sub

Private Sub Form_Current()
Dim SQL As String
SQL = "SELECT dbo_CustContact.HRID, [FirstName] & ' ' & UCase([LastName]) AS FName, dbo_CustContact.CustomerID " & _
            "FROM dbo_CustContact INNER JOIN dbo_HR ON dbo_CustContact.HRID = dbo_HR.HRID " & _
            "WHERE (((dbo_CustContact.CustomerID)= " & CStr(Nz(Me.CustomerID, 0)) & "));"
            Me.Controls("ComboSiteContactID").RowSource = SQL
            
            
End Sub

Private Sub Form_DblClick(Cancel As Integer)
    ' goto the SITE
    If Nz(Me.SiteID, 0) > 0 Then
        If Me.Dirty Then
            Me.Dirty = False
        End If
        mdlGoto.OpenFormAndCue "_Site", Me.SiteID, "[SiteID]"
    End If

End Sub