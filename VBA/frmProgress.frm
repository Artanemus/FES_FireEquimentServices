VERSION 5.00
Begin {C62A69F0-16DC-11CE-9E98-00AA00574A4F} frmProgress 
   Caption         =   "Update Progress"
   ClientHeight    =   3975
   ClientLeft      =   120
   ClientTop       =   465
   ClientWidth     =   8040
   OleObjectBlob   =   "frmProgress.frx":0000
   ShowModal       =   0   'False
   StartUpPosition =   1  'CenterOwner
End
Attribute VB_Name = "frmProgress"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
'@Folder("FES_Database")
Option Compare Database
Option Explicit

' UserForm progress bar
' MSForm.labels instead of Access.labels
Private fPrgBar As clsProgressBarUF
Private fAbort As Boolean

Public Property Get xMax() As Double
    xMax = 100
    If Not fPrgBar Is Nothing Then
        xMax = fPrgBar.Max
    End If
End Property

Public Property Get xAbort() As Boolean
    xAbort = fAbort
End Property

Public Property Get xFrontWidth() As Double
    xFrontWidth = fPrgBar.FrontWidth
End Property


Public Property Let xMax(ByVal RHS As Double)
    If Not fPrgBar Is Nothing Then
        fPrgBar.Max = RHS
    End If
End Property

Public Property Get xValue() As Double
    xValue = 0
    If Not fPrgBar Is Nothing Then
        xValue = fPrgBar.Value
    End If
End Property

Public Property Let xValue(ByVal RHS As Double)
    If Not fPrgBar Is Nothing Then
        fPrgBar.Value = RHS
    End If
End Property

Public Sub xClear()
    If Not fPrgBar Is Nothing Then
        ' after a call clear - use call initialise to reset the progress bar.
        fPrgBar.Clear
    End If
End Sub

Public Sub xInitialize()
    If Not fPrgBar Is Nothing Then
        fPrgBar.Initialize lblBack, lblFront, lblText
    End If
    fAbort = False
End Sub

Public Sub xIncrement()
    If Not fPrgBar Is Nothing Then
        fPrgBar.Increment
    End If
End Sub

Private Sub cmdCancel_Click()
    ' ABORT
    fAbort = True
End Sub


Private Sub UserForm_Initialize()
    Dim Msg As String
    On Error GoTo PROC_ERR
    ' customize with user company data
    lblFES.Caption = mdlCompany.GetCompanyCode
    ' create the UserForm progress bar
    Set fPrgBar = New clsProgressBarUF
    ' remove the design-time caption ...
    Me.lblBack.Caption = vbNullString
    Me.lblFront.Caption = vbNullString
    Me.lblText.Caption = vbNullString
    ' assign the controls to the UserForm progress bar
    fPrgBar.Initialize lblBack, lblFront, lblText
PROC_EXIT:
    On Error Resume Next
    Exit Sub
PROC_ERR:
    ' display the system error
    If err.Number <> 0 Then
        Msg = Me.Name & " Form_Load" & vbCrLf & _
              "Error # " & CStr(err.Number) & " was generated by " & err.Source & vbCrLf & err.Description
        MsgBox Msg, , "Error", err.HelpFile, err.HelpContext
    End If
End Sub

Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
'Cancel  An integer. Setting this argument to any value other than 0 stops the QueryClose event in all loaded
'           user forms and prevents the UserForm and application from closing.
'CloseMode   A value or constant indicating the cause of the QueryClose event.
    Cancel = 1
    fAbort = True
'If CloseMode = vbFormControlMenu Then
'    Cancel = True
'End If

End Sub

Private Sub UserForm_Terminate()
    ' terminate the user form ....
    Set fPrgBar = Nothing
End Sub



