Option Compare Database
Option Explicit

Const MainFrm As String = "_Survey"
Const SubFrm As String = "SF_SurveyFind"

Const PKey As String = "[SurveyOrderID]"
Const SearchFld As String = "[xAddress]"

Const ModuleName As String = "SurveyFind"

Private Sub Form_Load()
Dim v As Variant
    v = DLookup("[CompanyCode]", "dbo_CompanyInfo", "[CompanyInfoID] = 1")
    If Not IsNull(v) Then
        Me.lblCompanyCode.Caption = CStr(v)
    Else
        Me.lblCompanyCode.Caption = "FES"
    End If

End Sub

Private Sub Form_Open(Cancel As Integer)
    txtTextBox = vbNullString
    If Me.txtTextBox.Visible Then Me.txtTextBox.SetFocus
End Sub

Private Sub Form_Activate()
    If Me.txtTextBox.Visible Then Me.txtTextBox.SetFocus
End Sub

Public Sub Child_Called_DlbClick(Cancel As Integer)
    Form_DblClick Cancel
End Sub

Private Sub Form_DblClick(Cancel As Integer)
    cmdGOTO_Click
End Sub

Private Sub cmdClear_Click()
    Me.txtTextBox = vbNullString
    If Me.txtTextBox.Visible Then Me.txtTextBox.SetFocus
    ParamChanged
End Sub

Private Sub cmdGOTO_Click()
Dim strCriteria As String
Dim rst As DAO.Recordset
Dim v As Variant

Dim msg As String
On Error GoTo PROC_ERR
    
    v = Me.Form(SubFrm).Form.Controls(PKey)
    If (Nz(v, 0) > 0) Then
        If Not IsLoaded(MainFrm) Then
            DoCmd.OpenForm MainFrm, acNormal
        End If
        Forms(MainFrm).FilterOn = False
        Set rst = Forms(MainFrm).RecordsetClone
        strCriteria = PKey & " = " & CStr(v)
        rst.FindFirst strCriteria
        If Not rst.NoMatch Then
            Forms(MainFrm).Bookmark = rst.Bookmark
            Forms(MainFrm).SetFocus
            Exit Sub
        Else
            MsgBox "NoMatch :: Unable to bookmark ID.", , "Error finding booking..."
        End If
        Set rst = Nothing
    Else
        Beep
    End If
    
PROC_EXIT:
    On Error Resume Next
    Exit Sub

PROC_ERR:
    ' display the system error
    If Err.Number <> 0 Then
        msg = ModuleName & " cmdGOTO_Click Error # " & CStr(Err.Number) & " was generated by " & Err.Source & vbCrLf & Err.Description
        MsgBox msg, , "Error", Err.HelpFile, Err.HelpContext
    End If
    Resume PROC_EXIT

End Sub
    


Private Sub txtTextBox_Change()
    ParamChanged
    Me.Form(SubFrm).Form.CountRecords
End Sub

Private Sub SetSF_FilterOff()
    If Me.Form(SubFrm).Form.FilterOn Then
        Me.Form(SubFrm).Form.FilterOn = False
    End If
End Sub

Private Sub ParamChanged()
    Dim v As Variant, v2 As Variant
    Dim s As String
    Dim msg As String
    On Error GoTo PROC_ERR
    
    If Me.txtTextBox.Visible Then Me.txtTextBox.SetFocus
    s = Me.txtTextBox.Text
    
    If Len(s) = 0 Then
        lblSearchStatus.Caption = vbNullString
        lblSearchStatus.Visible = False
        SetSF_FilterOff
        Exit Sub
    End If
    If Len(Nz(s, vbNullString)) = 0 Then
        lblSearchStatus.Caption = "Enter a search string."
        lblSearchStatus.Visible = True
        SetSF_FilterOff
        Exit Sub
    End If
    

'    If Len(Trim(mTxt)) > 0 Then
'        s2 = "([FName] LIKE '%" + mTxt + "%')"
'    Else
'        s2 = "([FName] LIKE '%' OR [FName] IS NULL)"
'    End If
'
'    ' Include / Exclude Archived HR
'    If (Me.CheckHideArchivedHR <> True) Then
'        If Len(Nz(s2, "")) <> 0 Then
'            s2 = s2 + " AND [IsArchived] <> True "
'        Else
'            s2 = "[IsArchived] <> True "
'        End If
'    End If
    
    Me.Form(SubFrm).Form.Filter = SearchFld & " LIKE '%" & s & "%'"
    If Me.Form(SubFrm).Form.FilterOn = False Then
        Me.Form(SubFrm).Form.FilterOn = True
    End If
    ' no errors ... assert visibility
    lblSearchStatus.Visible = False
    
PROC_EXIT:
    If Me.txtTextBox.Visible Then Me.txtTextBox.SetFocus
    On Error Resume Next
    Exit Sub

PROC_ERR:
    ' display the system error
    If Err.Number <> 0 Then
        msg = "mdlHRFind ParamChanged" & vbCrLf & _
              "Error # " & CStr(Err.Number) & " was generated by " & Err.Source & vbCrLf & Err.Description
        MsgBox msg, , "Error", Err.HelpFile, Err.HelpContext
    End If
    Resume PROC_EXIT
    
    
End Sub

